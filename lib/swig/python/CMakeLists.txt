#
# Copyright (c) 2013 Nicholas Corgan (n.corgan@gmail.com)
#
# Distributed under the MIT License (MIT) (See accompanying file LICENSE.txt
# or copy at http://opensource.org/licenses/MIT)
#

FILE(TO_NATIVE_PATH "${CMAKE_SOURCE_DIR}/lib/swig" PKMNSIM_SWIG_SOURCE_DIR)
SET(PKMNSIM_SWIG_SOURCE_DIR ${PKMNSIM_SWIG_SOURCE_DIR} CACHE FILEPATH "PKMNsim SWIG source directory")

# Set Python install directory
execute_process(COMMAND ${PYTHON_EXECUTABLE} -c "
from distutils import sysconfig
print sysconfig.get_python_lib(plat_specific=True, prefix='')
" OUTPUT_VARIABLE PKMNSIM_PYTHON_DIR OUTPUT_STRIP_TRAILING_WHITESPACE
)
file(TO_CMAKE_PATH ${PKMNSIM_PYTHON_DIR} PKMNSIM_PYTHON_DIR)
SET(PKMNSIM_PYTHON_DIR ${PKMNSIM_PYTHON_DIR} CACHE FILEPATH "Python install directory")

INCLUDE(${SWIG_USE_FILE})

SET(PKMNSIM_PYTHON_INCLUDE_DIRS
    ${CMAKE_SOURCE_DIR}
    ${PKMNSIM_SWIG_SOURCE_DIR}
    ${Boost_INCLUDE_DIRS}
    ${SQLITE3_INCLUDE_DIR}
    ${PYTHON_INCLUDE_DIRS}
)
INCLUDE_DIRECTORIES(${PKMNSIM_PYTHON_INCLUDE_DIRS})

SET(CMAKE_SWIG_FLAGS -module pypkmnsim_swig)
FOREACH(dir ${PKMNSIM_PYTHON_INCLUDE_DIRS})
    LIST(APPEND CMAKE_SWIG_FLAGS "-I${dir}")
ENDFOREACH(dir ${PKMNSIM_PYTHON_INCLUDE_DIRS})

SET(PKMNSIM_PYTHON_LIBRARIES
    pkmnsim
    ${PYTHON_LIBRARIES}
)

SET_SOURCE_FILES_PROPERTIES(pypkmnsim_swig.i PROPERTIES CPLUSPLUS ON)

INCLUDE(UseSWIG)
SWIG_ADD_MODULE(pypkmnsim_swig python pypkmnsim_swig.i)
IF(CMAKE_COMPILER_IS_GNUCXX)
    SET_TARGET_PROPERTIES(${SWIG_MODULE_pypkmnsim_swig_REAL_NAME} PROPERTIES COMPILE_FLAGS "-std=c++0x")
ENDIF(CMAKE_COMPILER_IS_GNUCXX)
SWIG_LINK_LIBRARIES(pypkmnsim_swig ${PKMNSIM_PYTHON_LIBRARIES})

SET(PYPKMNSIM_LIB_SOURCES
    ${CMAKE_CURRENT_SOURCE_DIR}/__init__.py
    ${CMAKE_CURRENT_BINARY_DIR}/pypkmnsim_swig.py
)

#Set install instructions
INSTALL(
    FILES ${PYPKMNSIM_LIB_SOURCES}
    DESTINATION ${PKMNSIM_PYTHON_DIR}/PyPKMNsim
    COMPONENT "PyPKMNsim"
)
INSTALL(
    TARGETS ${SWIG_MODULE_pypkmnsim_swig_REAL_NAME}
    DESTINATION ${PKMNSIM_PYTHON_DIR}/PyPKMNsim
    COMPONENT "PyPKMNsim"
)
